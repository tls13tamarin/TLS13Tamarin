define(<!uniq!>, <!
lemma one_$1_per_tid [reuse]:
    "All tid #i #j. $1(tid)@i & $1(tid)@j ==> #i = #j"
!>)

define(<!one_of!>, <!
lemma $1_vs_$2 [reuse]:
    "All tid #i #j. $1(tid)@i & $2(tid)@j ==> F"
!>)


define(<!lemma_tid_invariant!>, <!
lemma tid_invariant [use_induction, reuse]:
  "All tid actor role #i. Instance(tid, actor, role)@i==>
      (Ex #j. Start(tid, actor, role)@j & (#j < #i))"
!>)

define(<!lemma_one_start_per_tid!>, <!
lemma one_start_per_tid [reuse]:
  "All tid actor actor2 role role2 #i #j. Start(tid, actor, role)@i & Start(tid, actor2, role2)@j ==>#i=#j"
!>)


/*
  TYPING LEMMAS
*/
define(<!lemma_cert_req_origin!>, <!
lemma cert_req_origin [typing]:
  "All certificate_request_context certificate_extensions keys #i.
    KU(senc{handshake_record('13', certificate_request_context, certificate_extensions)}keys)@i ==>
      (Ex #j. KU(certificate_request_context)@j & #j < #i) |
      (Ex #j tid actor role. running(CertReqCtxt, actor, role, certificate_request_context)@j & #j < #i)"
!>)

define(<!lemma_nst_source!>, <!
lemma nst_source [typing]:
  "All ticket ticket_age_add tkt_lt tkt_exts app_key #i.
    KU(senc{handshake_record('4', tkt_lt, ticket_age_add, ticket, tkt_exts)}app_key)@i ==>
      (Ex #j #k. KU(ticket)@j & KU(ticket_age_add)@k & #j < #i & #k < #i) |
      (Ex tid S #j. running_server(NST, ticket, ticket_age_add)@j & #j < #i)"
!>)


/*
  LEMMAS USED IN AUTH_HELPERS

*/

define(<!lemma_matching_nonces!>, <!
lemma matching_nonces [reuse]:
  "All tid tid2 actor actor2 role  nonces #i #j.
    running(Nonces, actor, role, nonces)@i &
    running2(Nonces, actor2, role, nonces)@j ==>
    tid = tid2 & actor = actor2"
!>)

define(<!lemma_consistent_nonces!>, <!
lemma consistent_nonces [reuse]:
  "All tid actor role nonces #i.
    commit(Nonces, actor, role, nonces)@i ==>
      Ex #j. running(Nonces, actor, role, nonces)@j"
!>)

define(<!lemma_invariant_nonces!>, <!
lemma invariant_nonces [reuse]:
  "All tid actor actor2 role role2 nonces nonces2 #i #j.
    running(Nonces, actor, role, nonces)@i &
    running(Nonces, actor2, role2, nonces2)@j ==> #i = #j"
!>)

define(<!lemma_matching_rms_nonces!>, <!
lemma matching_rms_nonces [reuse]:
  "All nonces tid tid2 actor actor2 peer peer2 rms messages #i #j.
    running(RMS, actor, 'client', peer, rms, messages)@i &
    running2(RMS, actor2, 'server', peer2, rms, messages)@j &
    commit2(Nonces, actor2, 'server', nonces)@j ==>
      Ex #a.
        commit(Nonces, actor, 'client', nonces)@a &
        #a < #i"

!>)

/*
  LEMMAS USED IN SECRET_HELPERS

*/

define(<!lemma_ku_extract!>, <!
lemma ku_extract [reuse, use_induction]:
  "All a b #i. KU(Extract(a, b))@i ==> Ex #j #k. KU(a)@j & KU(b)@k & #j < #i & #k < #i"
!>)

define(<!lemma_ku_expand!>, <!
lemma ku_expand [reuse, use_induction]:
"All secret label len #i. KU(Expand(secret, label, len))@i ==>
  (Ex #j. KU(secret)@j & #j < #i) |
  (not (Ex #k. KU(secret)@k & #k < #i) &
  (Ex actor #l. RevealPSK(actor, Expand(secret, label, len))@l & #l < #i))"
!>)

define(<!lemma_dh_chal_dual!>, <!
lemma dh_chal_dual [reuse]:
  "All tid tid2 actor actor2 g x y gx gy gxy #i #j #r.
      DHChal(g, x, y, gx, gy, gxy)@i & Instance(tid, actor, 'client')@i &
      DHChal(g, x, y, gx, gy, gxy)@j & Instance(tid2, actor2, 'server')@j &
      KU(gxy)@r
      ==>
      (Ex #p. (RevDHExp(tid, actor,  x)@p & #p < #r)) |
      (Ex #q. (RevDHExp(tid2, actor2, y)@q & #q < #r))"
!>)

define(<!lemma_ku_hs!>, <!
lemma ku_hs [reuse]:
  "All tid actor role es hs res_psk gxy #i #j.
    running(HS, actor, role, hs)@i &
    hs = HandshakeSecret &
    es = EarlySecret &
    KU(hs)@j ==>
      Ex #k #l. KU(gxy)@k & KU(res_psk)@l & #k < #j & #l < #j"
!>)

define(<!lemma_ku_ltk!>, <!
lemma ku_ltk [reuse]:
  "All actor ltkA #i #j.
    GenLtk(actor, ltkA)@i & KU(ltkA)@j ==>
      Ex #k. RevLtk(actor)@k & #k < #j"
!>)

define(<!lemma_ku_ss!>, <!
lemma ku_ss [reuse]:
  "All actor ssA #i #j.
    GenSS(actor, ssA)@i & KU(ssA)@j ==>
      Ex #k. RevSS(actor)@k & #k < #j"
!>)

define(<!lemma_ku_fresh_psk!>, <!
lemma ku_fresh_psk [reuse]:
  "All ticket res_psk #i #k.
      FreshPSK(ticket,res_psk)@i & KU(res_psk)@k ==>
        Ex actor #j.
          RevealPSK(actor, res_psk)@j & #j < #k"
!>)

define(<!lemma_hsms_derive!>, <!
lemma hsms_derive [reuse]:
  "All tid actor role hs ms ss #i.
    running(HSMS, actor, role, hs, ms, ss)@i ==>
      ms = MasterSecretWithSemiStatic"
!>)

define(<!lemma_posths_rms_weak!>, <!
// For any running(PostHS...) either the auth_status was set in the main HS and
// unchanged (along with the RMS), or there was post-hs auth, which means the
// peer's auth_status is 'auth', the actor is a server
lemma posths_rms_weak [reuse, use_induction]:
  "All tid actor role hs rms peer auth_status messages #i.
    running(PostHS, actor, role, hs, rms, peer, auth_status, messages)@i ==>
      Ex aas pas ms #j.
                running(RMS, actor, role, peer, rms, messages)@j &
                ms = MasterSecretWithSemiStatic & rms = resumption_master_secret() & #j < #i &
                auth_status = <aas, pas>"
!>)


define(<!lemma_posths_rms!>, <!
// For any running(PostHS...) either the auth_status was set in the main HS and
// unchanged (along with the RMS), or there was post-hs auth, which means the
// peer's auth_status is 'auth', the actor is a server
lemma posths_rms [reuse, use_induction]:
  "All tid actor role hs rms peer auth_status messages #i.
    running(PostHS, actor, role, hs, rms, peer, auth_status, messages)@i ==>
      Ex aas pas ms #j.
                running(RMS, actor, role, peer, rms, messages)@j &
                ms = MasterSecretWithSemiStatic & rms = resumption_master_secret() & #j < #i &
                auth_status = <aas, pas> &
      (
        (Ex aas2 #k. commit(Identity, actor, role, peer, <aas2, pas>)@k & #k < #i) |
        (Ex aas2 #k. commit(IdentityPost, actor, role, peer, <aas2, pas>)@k &
                role = 'server' & pas = 'auth' & (#k < #i | #k = #i)
        )
      )"
!>)

define(<!lemma_matching_transcripts_posths!>, <!
lemma matching_transcripts_posths [reuse]:
  "All tid tid2 actor peer actor2 peer2 role role2 rms rms2 messages #i #j.
    running(RMS, actor, role, peer2, rms, messages)@i &
    running2(RMS, peer, role2, actor2, rms2, messages)@j & not (role = role2) ==>
     rms = rms2"
!>)

define(<!lemma_matching_rms_posths!>, <!
lemma matching_rms_posths [reuse]:
  "All tid tid2 actor peer actor2 peer2 role role2 rms messages messages2 #i #j.
    running(RMS, actor, role, peer2, rms, messages)@i &
    running2(RMS, peer, role2, actor2, rms, messages2)@j & not (role = role2) ==>
     messages = messages2"
!>)

define(<!lemma_matching_rms_actors!>, <!
lemma matching_rms_actors [reuse]:
  "All tid tid2 actor peer actor2 peer2 role rms messages messages2 #i #j.
    running(RMS, actor, role, peer, rms, messages)@i &
    running2(RMS, actor2, role, peer2, rms, messages2)@j ==>
     actor = actor2 & tid = tid2"
!>)

define(<!lemma_matching_sessions!>, <!
lemma matching_sessions [reuse, use_induction, hide_lemma=posths_rms]:
  "All tid tid2 actor actor2 role role2 peer peer2 rms messages #i #j #k.
    running(RMS, actor, role, peer2, rms, messages)@i &
    running2(RMS, peer, role2, actor2, rms, messages)@j &
    not (role = role2) &
    KU(rms)@k ==>
      (Ex tid3 x #r. RevDHExp(tid3, actor, x)@r & #r < #i) |
      (Ex tid4 y #r. RevDHExp(tid4, peer, y)@r & #r < #j) |
      (Ex rms2 #r. RevealPSK(actor, rms2)@r & #r < #k) |
      (Ex rms2 #r. RevealPSK(peer, rms2)@r & #r < #k)"
!>)

define(<!lemma_sig_origin!>, <!
lemma sig_origin [reuse]:
  "All certificate certificate_request_context signature verify_data hs_key ss_key sig_messages ssA #i.
        KU(senc{Certificate, CertificateVerify, Finished}hs_key)@i & (signature = hmac(ss_key,sig_messages)) & DeriveFromSS(ssA, ss_key)@i ==>
      (Ex #j. KU(ssA)@j & #j < i) | (Ex #k. UseSS(ssA, signature)@k & #k < #i)"
!>)

define(<!lemma_auth_psk!>, <!
lemma auth_psk [reuse, use_induction, hide_lemma=posths_rms_weak]:
  "All tid tid2 actor actor2 role role2 peer peer2 rms messages aas #i #j #k.
    running(RMS, actor, role, peer2, rms, messages)@i &
    running2(RMS, peer, role2, actor2, rms, messages)@j &
    commit(Identity, actor, role, peer2, <aas, 'auth'>)@k &
    not (role = role2)
     ==>
      peer2 = peer |
      Ex #r. RevLtk(peer2)@r & #r < #k"
!>)

define(<!lemma_rms_derives_hs!>, <!
lemma rms_derives_hs[reuse,hide_lemma=sig_origin]:
  "All tid actor role peer hs ss rms messages #i #j #k #w.
     running(RMS, actor, role, peer, rms, messages)@j &
     commit(HS, actor, role, hs)@i &
     commit(SS, actor, role, ss)@w &
     KU(rms)@k ==>
         (Ex ms #l.
             ms = MasterSecretWithSemiStatic &
             KU(hs)@l & #l < #k) |
         (Ex #l. RevealPSK(actor, rms)@l & #l < #k) |
         (Ex #l. RevealPSK(peer, rms)@l & #l < #k)"

!>)

define(<!lemma_matching_hsms!>, <!
lemma matching_hsms [reuse]:
  "All tid actor role hs hs2 ms #i #j.
    commit(HS, actor, role, hs2)@i &
    running(HSMS, actor, role, hs, ms)@j ==>
      hs = hs2"
!>)

define(<!lemma_post_master_secret!>, <!
lemma post_master_secret [reuse, hide_lemma=posths_rms]:
  "All tid actor peer role hs rms aas messages #i #k.
    running(PostHS, actor, role, hs, rms, peer, <aas, 'auth'>, messages)@i &
    commit(HS, actor, role, hs)@i &
    commit(IdentityPost, actor, role, peer, <aas, 'auth'>)@i &
    KU(rms)@k ==>
      (Ex #r. RevLtk(peer)@r & #r < #i) |
      (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) |
      (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) |
      (Ex rms2 #r. RevealPSK(actor, rms2)@r & #r < #k) |
      (Ex rms2 #r. RevealPSK(peer, rms2)@r & #r < #k)"
!>)

define(<!lemma_invariant_post_hs!>, <!
lemma invariant_post_hs [reuse, use_induction, hide_lemma=posths_rms]:
  "All tid actor peer peer2 role hs hs2 rms rms2 as as2 msgs msgs2 #i #j.
    running(PostHS, actor, role, hs, rms, peer, as, msgs)@i &
    running(PostHS, actor, role, hs2, rms2, peer2, as2, msgs2)@j ==>
      peer = peer2 & rms = rms2 & msgs = msgs2 & hs = hs2"
!>)

define(<!lemma_handshake_secret!>, <!
lemma handshake_secret [reuse, use_induction, hide_lemma=posths_rms_weak]:
  "All tid actor peer role hs aas #i #k.
    commit(HS, actor, role, hs)@i &
    commit(Identity, actor, role, peer, <aas, 'auth'>)@i &
    KU(hs)@k ==>
        (Ex #r. RevLtk(peer)@r & #r < #i) |
        (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) |
        (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) |
        (Ex rms #r. RevealPSK(actor, rms)@r & #r < #k) |
        (Ex rms #r. RevealPSK(peer, rms)@r & #r < #k)"
!>)

define(<!lemma_pfs_handshake_secret!>, <!
lemma pfs_handshake_secret [reuse, hide_lemma=posths_rms_weak]:
  "All tid actor peer role hs aas psk_ke_mode #i #k.
    commit(HS, actor, role, hs)@i &
    running(Mode, actor, role, psk_ke_mode)@i &
    commit(Identity, actor, role, peer, <aas, 'auth'>)@i &
    KU(hs)@k &
    (not psk_ke_mode = psk_ke) ==>
        (Ex #r. RevLtk(peer)@r & #r < #i) |
        (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) |
        (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) |
        (Ex rms #r. RevealPSK(actor, rms)@r & #r < #i) |
        (Ex rms #r. RevealPSK(peer, rms)@r & #r < #i)"
!>)

/*
  LEMMAS USED IN LEMMA

*/
define(<!lemma_secret_session_keys!>, <!
lemma secret_session_keys [hide_lemma=sig_origin,hide_lemma=posths_rms]:
  "All tid actor peer kw kr pas #i.
      SessionKey(tid, actor, peer, <pas, 'auth'>, <kw, kr>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
    ==> not Ex #j. K(kr)@j"
!>)

define(<!lemma_secret_session_keys_ss!>, <!
lemma secret_session_keys_ss [hide_lemma=sig_origin,hide_lemma=posths_rms]:
  "All tid actor peer kw kr pas #i.
      SessionKey(tid, actor, peer, <pas, 'auth'>, <kw, kr>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex #r. RevSS(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex tid5 s #r. RevDHExp(tid5, peer, s)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
    ==> not Ex #j. K(kr)@j"
!>)

define(<!lemma_secret_session_keys_pfs!>, <!
lemma secret_session_keys_pfs [hide_lemma=sig_origin,hide_lemma=posths_rms]:
  "All tid actor peer role kw kr pas psk_ke_mode #i.
      SessionKey(tid, actor, peer, <pas, 'auth'>, <kw, kr>)@i &
      running(Mode, actor, role, psk_ke_mode)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(peer, rms)@r & #r < #i) &
      not (psk_ke_mode = psk_ke)
    ==> not Ex #j. K(kr)@j"
!>)

define(<!lemma_session_key_agreement!>, <!
lemma session_key_agreement [hide_lemma=sig_origin]:
  "All tid tid2 actor peer actor2 peer2 nonces keys keys2 cas as2 #i #j #k #l.
     SessionKey(tid, actor, peer2, <cas, 'auth'>, keys)@i &
     running(Nonces, actor, 'client', nonces)@j &
     SessionKey(tid2, peer, actor2, as2, keys2)@k &
     running2(Nonces, peer, 'server', nonces)@l &
      // not (Ex #r. RevLtk(actor)@r & #r < #i) &
      not (Ex #r. RevLtk(peer)@r & #r < #i & #r < #k) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i & #r < #k) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i & #r < #k) &
      not (Ex rms #r. RevealPSK(actor, rms)@r & #r < #i & #r < #k) &
      not (Ex rms #r. RevealPSK(peer, rms)@r & #r < #i & #r < #k)
      ==>
       keys = keys2"
!>)

define(<!lemma_unique_session_keys!>, <!
lemma unique_session_keys:
  "All tid tid2 actor peer peer2 kr kw as as2 #i #j.
     SessionKey(tid, actor, peer, as, <kr, kw>)@i &
     SessionKey(tid2, actor, peer2, as2, <kr, kw>)@j
      ==>
        #i = #j"
!>)

define(<!lemma_entity_authentication!>, <!
/*
  Unilateral (entity) authentication
*/
lemma entity_authentication [reuse, use_induction]:
  "All tid actor peer nonces cas #i.
      commit(Nonces, actor, 'client', nonces)@i & commit(Identity, actor, 'client', peer, <cas, 'auth'>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
          ==> (Ex tid2 #j. running2(Nonces, peer, 'server', nonces)@j & #j < #i)"
!>)

define(<!lemma_transcript_agreement!>, <!
/*
  Integrity of handshake messages
*/
lemma transcript_agreement [reuse]:
  "All tid actor peer transcript cas #i.
      commit(Transcript, actor, 'client', transcript)@i & commit(Identity, actor, 'client', peer, <cas, 'auth'>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
          ==> (Ex tid2 #j. running2(Transcript, peer, 'server', transcript)@j & #j < #i)"
!>)

define(<!lemma_mutual_entity_authentication!>, <!
/*
  Mutual (entity) authentication
*/
lemma mutual_entity_authentication [reuse, use_induction]:
  "All tid actor peer nonces sas #i.
      commit(Nonces, actor, 'server', nonces)@i & commit(Identity, actor, 'server', peer, <sas, 'auth'>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
          ==> (Ex tid2 #j. running2(Nonces, peer, 'client', nonces)@j & #j < #i)"
!>)

define(<!lemma_mutual_transcript_agreement!>, <!
/*
  Integrity of handshake messages
*/
lemma mutual_transcript_agreement [reuse]:
  "All tid actor transcript peer sas #i.
      commit(Transcript, actor, 'server', transcript)@i & commit(Identity, actor, 'server', peer, <sas, 'auth'>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
          ==> (Ex tid2 #j. running2(Transcript, peer, 'client', transcript)@j & #j < #i)"
!>)


define(<!lemma_mutual_injective_entity_authentication!>, <!
/*
  Mutual (entity) authentication
*/
lemma injective_mutual_entity_authentication [reuse, hide_lemma=posths_rms]:
  "All tid actor peer role nonces aas #i.
      commit(Nonces, actor, role, nonces)@i & commit(Identity, actor, role, peer, <aas, 'auth'>)@i &
      not (Ex #r. RevLtk(peer)@r & #r < #i) &
      not (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) &
      not (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) &
      not (Ex rms #r. RevealPSK(actor, rms)@r) &
      not (Ex rms #r. RevealPSK(peer, rms)@r)
          ==>
          Ex role2 tid2 #j. running2(Nonces, peer, role2, nonces)@j & #j < #i & not role = role2 &
          (All tid3 peer2 #k. running3(Nonces, peer2, role2, nonces)@k ==> #k = #j)"
!>)

/*
  LEMMAS USED IN DH_INJ

*/

define(<!lemma_dh_exp_invariant!>, <!
lemma dh_exp_invariant  [use_induction, reuse]:
  "All tid actor x #i. RevDHExp(tid, actor, x)@i ==>
    Ex #j. DH(tid, actor, x)@j & #j < i"
!>)

define(<!lemma_one_dh_per_x!>, <!
lemma one_dh_per_x  [reuse]:
  "All tid tid2 x actor actor2 #i #j.
    DH(tid, actor, x)@i & DH(tid2, actor2, x)@j ==> #i = #j"
!>)

define(<!lemma_rev_dh_ordering!>, <!
lemma rev_dh_ordering  [reuse, use_induction]:
  "All tid actor x #j.
    DeleteDH(tid, actor, x)@j==>
      ((Ex #i. DH(tid, actor, x) @ i & i < j) &
       (All #r. RevDHExp(tid, actor, x) @ r  ==>  r < j))"
!>)

define(<!lemma_rev_dh_before_hs!>, <!
lemma rev_dh_before_hs  [reuse]:
  "All tid actor role hs x #i #j.
    running(HS, actor, role, hs)@j &
    RevDHExp(tid, actor, x)@i ==>
    #i < #j"
!>)
//NTS: RevDH < RRMS
//May have to add tid's to RevDH.
